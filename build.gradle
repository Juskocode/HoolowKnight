plugins {
    id 'java'
    id 'application'
    id "net.ltgt.errorprone" version "4.1.0"
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
    // Creates a self-contained (fat) JAR so you can run the game with a single file
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'HollowKnight'
version = '1.0'


repositories {
    mavenCentral()
}

dependencies {
    // Add the main implementation dependencies
    implementation 'com.googlecode.lanterna:lanterna:3.1.1'

    // Test dependencies
    testImplementation platform('org.junit:junit-bom:5.10.0')  // JUnit BOM to handle JUnit versions
    testImplementation 'org.junit.jupiter:junit-jupiter'  // JUnit 5 Jupiter API
    testImplementation 'org.mockito:mockito-core:3.7.7'    // Mockito for mocking
    testImplementation 'net.jqwik:jqwik:1.5.1'  // Property-based testing
    errorprone "com.google.errorprone:error_prone_core:2.30.0" // to locate code smells

}

application {
    mainClass.set('HollowKnight.Game')
}


test {
    useJUnitPlatform {
        includeEngines ('junit-jupiter', 'jqwik')
    }
    testLogging {
        exceptionFormat = 'full'
    }
    finalizedBy jacocoTestReport
}

compileJava {
    options.compilerArgs << "-Werror"
}


// Ensure generated JARs have a Main-Class so they can be launched easily
jar {
    manifest {
        attributes 'Main-Class': 'HollowKnight.Game'
    }
}

// Configure the fat JAR task produced by the Shadow plugin
// Usage: ./gradlew shadowJar
// Then run: java -jar build/libs/${rootProject.name}-${version}-all.jar
shadowJar {
    archiveClassifier.set('all')
    manifest {
        attributes 'Main-Class': 'HollowKnight.Game'
    }
}


jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

pitest {
    junit5PluginVersion = '0.12'
    jvmArgs = ['-Xmx2g', '-Xms1g', '-XX:MaxPermSize=1g', '-XX:+UseG1GC']
}


